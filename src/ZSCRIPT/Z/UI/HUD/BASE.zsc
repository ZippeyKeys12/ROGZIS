class ZStatusBar:BaseStatusBar{
    const NOTIFSCALE=.4;

    ZMarine PlayerInst;

    ZBroadcaster Broadcaster;

    ZStats Stats;

    Array<ZElement> Elements;

    Av_Matrix2 World2Clip;

    override
    void NewGame(){
        super.NewGame();
        Broadcaster=ZBroadcaster.Instance();
    }

    override
    void Init(){
        super.Init();
        SetSize(0, 640, 480);
    }

    override
    void DrawAutomapHUD(double ticFrac)
    {DoDrawAutomapHUD(Font.CR_RED, Font.CR_UNTRANSLATED);}

    override
    void Draw (int State, double TicFrac){
        super.Draw(State, TicFrac);
        BeginHUD();
        if(!Stats)
            Stats=PlayerInst.Stats;
        World2Clip=Av_Matrix2.WorldToClip(PlayerInst.Pos, PlayerInst.Angle, PlayerInst.Pitch, PlayerInst.Roll, CPlayer.FoV);
        DrawStats();
        DrawDebug();
        int VWidth=Screen.GetWidth()*.2,
            VHeight=Screen.GetHeight()*.2;
        Screen.DrawText(SmallFont, Font.CR_GOLD, VWidth/2-SmallFont.StringWidth("Wassup!!!")/2, 10, "Wassup!!!", DTA_VirtualWidth, VWidth, DTA_VirtualHeight, VHeight);
        for(int i=0; i<Elements.Size(); i++)
            Elements[i].OnDraw();
    }

    virtual
    Vector2 OnActor(Actor mo, Vector3 Offset=(0, 0, 0)){
        Vector3 Pos=mo.Pos+Offset;
        Pos.Z+=PlayerInst.ViewHeight+PlayerInst.Pos.Z-CPlayer.ViewZ;
        Vector3 Vec3=World2Clip.Transform_3D(Pos).ToV3();
        return ((1+Vec3.X)*Screen.GetWidth()/2, (1-Vec3.Y)*Screen.GetHeight()/2);
    }

    override
    void Tick(){
        super.Tick();
        UpdateStats();
        for(int i=0; i<Elements.Size(); i++)
            Elements[i].Tick();
    }
}
