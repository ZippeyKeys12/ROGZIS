enum ZE_BoxSides{
    BOX_NONE,
    BOX_TR  =   1<<0,
    BOX_TL  =   1<<1,
    BOX_BR  =   1<<2,
    BOX_BL  =   1<<3,
    BOX_ALL =   BOX_TR|BOX_TL|BOX_BR|BOX_BL
}

class ZBox:ZElement{
    ZE_BoxSides Sides;

    double Alpha;

    TextureID Corner,
              Edge,
              Fill;

    static
    ZBox Create(Vector2 Pos, Vector2 Size, double Alpha=1, ZE_BoxSides Sides=BOX_ALL, ZComponent Parent=null){
        ZBox Result=ZBox(ZElement.Create('ZBox', Pos, Size, Parent));
        Result.Sides=Sides;
        Result.Alpha=Alpha;
        Result.Corner=Av_Texture.Misc('HUDCNR');
        Result.Edge=Av_Texture.Misc('HUDEDGE');
        Result.Fill=Av_Texture.Misc('HUDFILL');
        return Result;
    }

    override
    void Draw(){
        Vector2 CornerSize=TexMan.GetScaledSize(Corner);
        bool TL=Sides & BOX_TL,
             TR=Sides & BOX_TR,
             BL=Sides & BOX_BL,
             BR=Sides & BOX_BR;
        double LeftLength=AABB.Size.Y-CornerSize.Y*(TL+BL)-!TL-!BL,
               RightLength=AABB.Size.Y-CornerSize.Y*(TR+BR)-!TR-!BR,
               LeftOffset=!TL+TL*CornerSize.Y,
               RightOffset=!TR+TR*CornerSize.Y,
               LenMinusCnr=AABB.Size.X-CornerSize.X,
               HgtMinusCnr=AABB.Size.Y-CornerSize.Y,
               CnrLenMinus1=CornerSize.X-1;
        Drawer.DrawTexture(Edge, Width: AABB.Size.X-CornerSize.X*(TL+TR)-!TL-!TR, Alpha: Alpha, Pos: (!TL+TL*CornerSize.X, 0));
        Drawer.DrawTexture(Fill, AABB.Size.Y-2*(TL || TR || BL || BR), AABB.Size.X-2*CornerSize.X, Alpha: Alpha, Pos: (CornerSize.X, 1));
        Drawer.DrawTexture(Edge, LeftLength, Alpha: Alpha, Pos: (0, LeftOffset));
        Drawer.DrawTexture(Fill, LeftLength, CnrLenMinus1, Alpha: Alpha, Pos: (1, LeftOffset));
        Drawer.DrawTexture(Edge, RightLength, Alpha: Alpha, Pos: (AABB.Size.X-1, RightOffset));
        Drawer.DrawTexture(Fill, RightLength, CnrLenMinus1, Alpha: Alpha, Pos: (LenMinusCnr, RightOffset));
        Drawer.DrawTexture(Edge, Width: AABB.Size.X-CornerSize.X*(BL+BR)-!BL-!BR, Alpha: Alpha, Pos: (!BL+BL*CornerSize.X, AABB.Size.Y-1));
        if(Sides!=BOX_NONE){
            if(TL)
                Drawer.DrawTexture(Corner, Alpha: Alpha, FlipX: true);
            if(TR)
                Drawer.DrawTexture(Corner, Alpha: Alpha, Pos: (LenMinusCnr, 0));
            if(BL)
                Drawer.DrawTexture(Corner, Alpha: Alpha, FlipX: true, FlipY: true, Pos: (0, HgtMinusCnr));
            if(BR)
                Drawer.DrawTexture(Corner, Alpha: Alpha, FlipY: true, Pos: (LenMinusCnr, HgtMinusCnr));
        }
    }
}