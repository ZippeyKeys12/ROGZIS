class ZMatrix3:ZObject{
    private
    uint __Height__,
         __Width__,
         __Depth__;

    Array<double> Values;

    static
    ZMatrix3 Create(uint Height, uint Width, uint Depth, bool Ones=false){
        ZMatrix3 Result=new('ZMatrix3');
        if(Height<=0 || Width<=0 || Depth<=0)
            Result.Fatal("Attempted to create a non-positively-sized matrix");
        Result.__Height__=Height;
        Result.__Width__=Width;
        Result.__Depth__=Depth;
        for(int i=0; i<Height*Width*Depth; i++)
            Result.Values.Push(Ones);
        return Result;
    }

    static
    ZMatrix3 Randomized(uint Height, uint Width, uint Depth, double Floor=-1, double Ceiling=1, int Seed=0)
    {return Create(Height, Width, Depth, true).Noise(Floor, Ceiling, Seed);}

    //////////
    // Info //
    //////////

    virtual
    double Get(uint Row, uint Column, uint Layer) const
    {return Values[Row+__Width__*(Column+__Height__*Layer)];}

    ///////////////////
    // Manipulations //
    ///////////////////

    virtual
    void Set(uint Row, uint Column, uint Layer, double Value)
    {Values[Row+__Width__*(Column+__Height__*Layer)]=Value;}

    virtual
    ZMatrix3 Add(ZMatrix3 Other) const{
        if(__Height__!=Other.__Height__ || __Width__!=Other.__Width__ || __Depth__!=Other.__Depth__)
            Fatal("Attempted to add differently-sized matrices");
        ZMatrix3 Result=Create(__Height__, __Width__, __Depth__);
        for(int i=0; i<Values.Size(); i++)
            Result.Values[i]=Values[i]+Other.Values[i];
        return Result;
    }

    virtual
    ZMatrix3 Sub(ZMatrix3 Other) const{
        if(__Height__!=Other.__Height__ || __Width__!=Other.__Width__ || __Depth__!=Other.__Depth__)
            Fatal("Attempted to subtract differently-sized matrices");
        ZMatrix3 Result=Create(__Height__, __Width__, __Depth__);
        for(int i=0; i<Values.Size(); i++)
            Result.Values[i]=Values[i]-Other.Values[i];
        return Result;
    }

    virtual
    ZMatrix3 Mul(ZMatrix3 Other) const{
        if(__Height__!=Other.__Height__ || __Width__!=Other.__Width__ || __Depth__!=Other.__Depth__)
            Fatal("Attempted to multiply differently-sized matrices");
        ZMatrix3 Result=Create(__Height__, __Width__, __Depth__);
        for(int i=0; i<Values.Size(); i++)
            Result.Values[i]=Values[i]*Other.Values[i];
        return Result;
    }

    virtual
    ZMatrix3 Div(ZMatrix3 Other) const{
        if(__Height__!=Other.__Height__ || __Width__!=Other.__Width__ || __Depth__!=Other.__Depth__)
            Fatal("Attempted to divide differently-sized matrices");
        ZMatrix3 Result=Create(__Height__, __Width__, __Depth__);
        for(int i=0; i<Values.Size(); i++)
            Result.Values[i]=Values[i]/Other.Values[i];
        return Result;
    }

    virtual
    ZMatrix3 Map(ZF_Base Function) const{
        ZMatrix3 Result=Create(__Height__, __Width__, __Depth__);
        for(int i=0; i<__Height__*__Width__*__Depth__; i++)
            Result.Values[i]=Function.Apply(Values[i]);
        return Result;
    }

    virtual
    ZMatrix3 SMul(double Multiplier) const{
        ZMatrix3 Result=Create(__Height__, __Width__, __Depth__);
        for(int i=0; i<Values.Size(); i++)
            Result.Values[i]=Values[i]*Multiplier;
        return Result;
    }

    virtual
    ZMatrix3 SDiv(double Divisor) const{
        ZMatrix3 Result=Create(__Height__, __Width__, __Depth__);
        for(int i=0; i<Values.Size(); i++)
            Result.Values[i]=Values[i]/Divisor;
        return Result;
    }

    virtual
    ZMatrix3 SExponent(double Degree){
        ZMatrix3 Result=Create(__Height__, __Width__, __Depth__);
        for(int i=0; i<Values.Size(); i++)
            Result.Values[i]=Values[i]**Degree;
        return Result;
    }

    virtual
    ZMatrix3 Noise(double Floor, double Ceiling, int Seed=0) const{
        if(!Seed)
            Seed=ZUtil.RandSeed();
        SetRandomSeed[NoiseyBoi](Seed);
        ZMatrix3 Result=Create(__Height__, __Width__, __Depth__);
        for(int Index=0; Index<Values.Size(); Index++)
            Result.Values[Index]=Values[Index]*FRandom[NoiseyBoi](Floor, Ceiling);
        return Result;
    }

    ////////////////
    // Transforms //
    ////////////////

    // TODO: Fix
    // virtual
    // ZMatrix2_G ToSM2(){
    //     if(__Depth__!=2)
    //         Fatal("Attempted to convert ZMatrix3 to ZMatrix2_G: Is not 2 deep");
    //     ZMatrix2_G Result=ZMatrix2_G.Create(__Height__, __Width__);
    //     Result.Values.Copy(Values);
    //     return Result;
    // }

    virtual
    ZMatrix3 Clone() const{
        ZMatrix3 Result=Create(__Height__, __Width__, __Depth__);
        Result.Values.Copy(Values);
        return Result;
    }
}