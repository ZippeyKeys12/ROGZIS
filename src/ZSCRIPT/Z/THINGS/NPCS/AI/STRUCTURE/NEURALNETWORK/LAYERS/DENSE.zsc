class ZNL_Dense:ZNeuralLayer{
    private
    uint Inputs,
         Outputs;

    private
    double LearningRate;

    private
    Av_Matrix2 In2Out,
            Bias;

    static
    ZNL_Dense Create(uint Inputs, uint Outputs, double LearningRate=.1){
        ZNL_Dense Result=new('ZNL_Dense');
        Result.Inputs=Inputs;
        Result.Outputs=Outputs;
        Av_Gaussian StdNormDist=Av_Gaussian.Create();
        Result.In2Out=Av_Matrix2.Create(Outputs, Inputs);
        for(int Row=0; Row<Outputs; Row++)
            for(int Column=0; Column<Inputs; Column++)
                Result.In2Out.Set(Row, Column, StdNormDist.Generate());
        Result.Bias=Av_Matrix2.Create(Outputs, 1);
        for(int Row=0; Row<Outputs; Row++)
            Result.Bias.Set(Row, 0, StdNormDist.Generate());
        Result.LearningRate=LearningRate;
        return Result;
    }

    override
    Av_Matrix2 Get(Av_Matrix2 Input)
    {return Activation(In2Out.M2Mul(Input).Add(Bias));}

    override
    Av_Matrix2 Weights()
    {return In2Out.Clone();}

    override
    Av_Matrix2 Train(Av_Matrix2 Input, Av_Matrix2 LastError, Av_Matrix2 LastWeights, bool Last){
        Av_Matrix2 Error=(Last)?LastError:(LastWeights.Transpose().M2Mul(LastError)),
                Gradient=Activation(Get(Input), true)
                        .Mul(Error)
                        .SMul(LearningRate),
                DeltaOutput=Gradient.M2Mul(Input.Transpose());
        In2Out=In2Out.Add(DeltaOutput);
        Bias=Bias.Add(Gradient);
        return Error;
    }
}