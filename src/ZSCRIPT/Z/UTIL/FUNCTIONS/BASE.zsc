class ZF_Linear:ZF_Base{}

class ZF_Base:ZObject{
    double Misc;

    ZF_Base Child;

    static
    ZF_Base Create(Name Type, double Misc=0){
        Type='ZF_'..Type;
        if(!ZUtil.ClassExists(Type)){
            Cl_Util.Log('', LOG_Fatal, Type.." does not exist");
            return null;
        }else{
            ZF_Base Result=ZF_Base(new(Type));
            Result.Misc=Misc;
            return Result.Init();
        }
    }

    virtual
    ZF_Base Init()
    {return self;}

    virtual
    double Apply(double X) const{
        if(Child)
            X=Child.Apply(X);
        return X;
    }

    virtual
    ZF_Base Attach(ZF_Base Function){
        if(Child)
            Error("Function already has child");
        Child=Function;
        return self;
    }

    virtual
    void Detach()
    {Child=null;}

    ////////////////
    // Operations //
    ////////////////

    ZF_Base Add(ZF_Base Other)
    {return ZF_Base(ZF_Operator.Create('Add', self, Other));}

    ZF_Base Sub(ZF_Base Other)
    {return ZF_Base(ZF_Operator.Create('Subtract', self, Other));}

    ZF_Base Mul(ZF_Base Other)
    {return ZF_Base(ZF_Operator.Create('Multiply', self, Other));}

    ZF_Base Div(ZF_Base Other)
    {return ZF_Base(ZF_Operator.Create('Divide', self, Other));}

    ZF_Base SAdd(double Constant)
    {return Add(ZF_Base.Create('Constant', Constant));}

    ZF_Base SSub(double Constant)
    {return Sub(ZF_Base.Create('Constant', Constant));}

    ZF_Base SMul(double Constant)
    {return Mul(ZF_Base.Create('Constant', Constant));}

    ZF_Base SDiv(double Constant)
    {return Div(ZF_Base.Create('Constant', Constant));}

    /////////////////////
    // Transformations //
    /////////////////////

    ZF_Base VShift(double Value)
    {return SAdd(Value);}

    // TODO: Deep copy for non-destructive operations
    ZF_Base HShift(double Value){
        Array<ZF_Base> Stack;
        Stack.Push(Clone());
        bool HasLeaf;
        for(int i=0; i<Stack.Size();i++){
            HasLeaf=false;
            if(Stack[i].Child){
                Stack.Push(Stack[i].Child);
                HasLeaf=true;
            }
            if(Stack[i] is 'ZF_Operator'){
                Stack.Push(ZF_Operator(Stack[i]).Main);
                Stack.Push(ZF_Operator(Stack[i]).Operand);
                HasLeaf=true;
            }
            if(HasLeaf){
                Stack.Delete(i);
                i--;
            }
        }
        for(int i=0; i<Stack.Size(); i++)
            Stack[i].Attach(Create('Linear').SAdd(-Value));
        return self;
    }

    ZF_Base VDilate(double Value)
    {return SMul(Value);}

    ZF_Base HDilate(double Value){
        Array<ZF_Base> Stack;
        Stack.Push(Clone());
        bool HasLeaf;
        for(int i=0; i<Stack.Size();i++){
            HasLeaf=false;
            if(Stack[i].Child){
                Stack.Push(Stack[i].Child);
                HasLeaf=true;
            }
            if(Stack[i] is 'ZF_Operator'){
                Stack.Push(ZF_Operator(Stack[i]).Main);
                Stack.Push(ZF_Operator(Stack[i]).Operand);
                HasLeaf=true;
            }
            if(HasLeaf){
                Stack.Delete(i);
                i--;
            }
        }
        for(int i=0; i<Stack.Size(); i++)
            Stack[i].Attach(Create('Linear').SMul(1/Value));
        return self;
    }

    virtual
    ZF_Base Clone() const{
        ZF_Base Result=ZF_Base(new(GetClassName()));
        Result.Misc=Misc;
        Result.Child=Child.Clone();
        return Result.Init();
    }
}